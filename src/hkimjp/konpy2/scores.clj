(ns hkimjp.konpy2.scores
  (:require
   [clojure.string :as str]
   [hiccup2.core :as h]
   [ring.util.anti-forgery :refer [anti-forgery-field]]
   [taoensso.telemere :as t]
   [hkimjp.datascript :as ds]
   [hkimjp.konpy2.response :refer [page hx]]
   [hkimjp.konpy2.util :refer [user btn]]))

(def ^:private answered
  '[:find ?e
    :in $ ?author
    :where
    [?e :answer/status "yes"]
    [?e :author ?author]])

(def ^:private sent
  '[:find ?e
    :in $ ?author
    :where
    [?e :comment/status "yes"]
    [?e :author ?author]])

(def ^:private received
  '[:find ?e ?pt
    :in $ ?author
    :where
    [?e :comment/status "yes"]
    [?e :to ?a]
    [?a :author ?author]
    [?e :pt ?pt]])

(defn- score [sym coll target]
  [:div
   (for [[e _] coll]
     [:span.hover:underline
      {:hx-get    (str "/k/score/" e)
       :hx-target (str "#" target)
       :hx-swap   "innerHTML"} sym])])

; â˜€ï¸ğŸŒ¥ï¸â›…ï¸ğŸŒ§ï¸ğŸ’§â˜‚ï¸â˜ï¸â¤ï¸ğŸ’›ğŸ”´ğŸ’šğŸ©µğŸ©¶ğŸŸ¢ğŸ”¸â—¾ï¸

(defn- div-score [ABC received]
  (let [pict  {"A" "â¤ï¸", "B" "ğŸ’š","C" "ğŸ©¶"}]
    [:div
     [:div.flex
      [:div ABC ": "]
      (score (pict ABC) (filter #(= ABC (second %)) received) ABC)]
     [:div.mx-4 {:id ABC}]]))

(defn- week-num [e]
  (let [record (ds/pl e)]
    (t/log! {:level :debug :data record})
    (try
      (if (some? (:week record))
        (str (:week record) "-" (:num record))
        (week-num (:to record)))
      (catch Exception ex
        (t/log! :error (.getMessage ex))
        nil))))

(defn hx-show [{{:keys [e]} :path-params}]
  (t/log! {:level :info :id "hx-show"})
  (let [e (parse-long e)
        submit (ds/pl e)]
    (hx [:div
         [:div [:span.font-bold "problem: "] (week-num e)]
         [:div [:span.font-bold "updated: "] (:updated submit)]
         [:pre.border-1 (or (:comment submit) (:answer submit))]
         [:br]])))

(defn- section [thing sym label target]
  [:div
   [:div.font-bold label]
   [:div.mx-4 (score sym thing target)]
   [:div.mx-4 {:id target}]])

(defn- peep-section []
  [:div
   [:div.font-bold.my-4 "Peep other student's score"]
   [:form
    (h/raw (anti-forgery-field))
    [:input.border-1.p-2 {:name "user"}]
    [:buttn {:class btn
             :hx-post    "/k/scores/peep"
             :hx-target "#peep"
             :hx-swap   "innerHTML"} "peep"]]
   [:div#peep]])

(defn hx-peep [{params :params}]
  (t/log! {:level :info :id "hx-peep" :data params})
  (hx [:div (str "data: " params)]))

(defn scores [request]
  (let [author   (user request)
        answered (sort (ds/qq answered author))
        sent     (sort (ds/qq sent author))
        received (sort (ds/qq received author))]
    (t/log! {:level :info :msg (str "scores " author)})
    (page
     [:div.m-4
      [:div.text-2xl (format "Scores (%s)" author)]
      [:p "å¤±ã£ãŸå¹³å¸¸ç‚¹ã¯å–ã‚Šè¿”ã›ãªã„ã€‚æ—¥é ƒã‹ã‚‰å–ã‚Šçµ„ã¾ãªã„ã¨å¹³å¸¸ç‚¹ãŒãªããªã‚‹ã€‚"]
      [:p "konpy ã®å‡ºé¡Œã¯é€±å¹³å‡6ã¤ã®äºˆå®šã€‚ä¸€é¡Œè§£ã„ãŸã‚‰3å€‹ã¯ä»–ã®å›ç­”èª­ã‚“ã§ã‚³ãƒ¡ãƒ³ãƒˆã—ãªã•ã„ã€‚"]
      [:br]
      (section answered "ğŸ’ª" "Your Answers" "answered")
      (section sent "ğŸ˜ƒ" "Comments Sent" "sent")
      [:div.font-bold.my-4 "Comments Received"]
      [:div.mx-4
       (for [sc ["A" "B" "C"]]
         (div-score sc received))]
      ; peep section, 0.3.13
      (peep-section)])))

